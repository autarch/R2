<p>
Found <% $count %> <% $search->has_restrictions() ? 'matching' : q{} %> <% PL_N( $search->result_type_string(), $count ) %>.
</p>

% if ($count) {
<table class="standard-table search-results">
  <thead>
    <tr class="pager">
      <th colspan="<% $colspan %>">
%   if ($pager) {
%     if ( $pager->previous_set() ) {
        <a href="<% $search->new_uri( order_by => $search->order_by(), reverse_order => $search->reverse_order() ) %>">1</a>
        <a href="<% $search->new_uri( order_by => $search->order_by(), reverse_order => $search->reverse_order(), page => $pager->previous_set() ) %>">...</a>
%     }
%     for my $page ( @{ $pager->pages_in_set() } ) {
%       if ($page) {
%         if ( $page == $search->page() ) {
        <strong><% $page %></strong>
%         } else {
        <a href="<% $search->new_uri( order_by => $search->order_by(), reverse_order => $search->reverse_order(), page => $page ) %>"><% $page %></a>
%         }
%       }
%     }
%     if ( $pager->next_set() ) {
        <a href="<% $search->new_uri( order_by => $search->order_by(), reverse_order => $search->reverse_order(), page => $pager->next_set() ) %>">...</a>
        <a href="<% $search->new_uri( order_by => $search->order_by(), reverse_order => $search->reverse_order(), page => $pager->last_page() ) %>"><% $pager->last_page() %></a>
%     }
%   }
      </th>
    </tr>
    <tr>
%   if ( $search->includes_multiple_contact_types() ) {
      <th></th>
%   }
      <th><&| /lib/search/sortable-header.mas, search => $search &>Name</&></th>
      <th>Location</th>
      <th><&| /lib/search/sortable-header.mas, search => $search, order_by => 'email_address' &>Email</&></th>
      <th>Activity</th>
      <th><&| /lib/search/sortable-header.mas, search => $search &>Created</&></th>
      <th><&| /lib/search/sortable-header.mas, search => $search &>Modified</&></th>
% if ($can_edit) {
      <th></th>
      <th></th>
% }
    </tr>
  </thead>

%   while ( my $contact = $contacts->next() ) {
<& .contact-row, contact => $contact, row_class => $contacts->index() % 2 ? 'odd' : 'even', search => $search, can_edit => $can_edit &>
%   }
</table>
% }

<%args>
$search
</%args>

<%init>
my $count = $search->count();

my $contacts;
$contacts = $search->contacts() if $count;

my $pager;
$pager = $search->pager() if $count && $count > $search->limit();

my $can_edit = $c->user()->can_edit_contacts( account => $c->account() );

my $colspan = $search->includes_multiple_contact_types() ? 7 : 6;
$colspan += 2 if $can_edit;
</%init>

<%def .contact-row>
  <tbody>
    <tr class="<% $row_class %>">
% if ( $search->includes_multiple_contact_types() ) {
      <td class="icon">
        <& /lib/contact-view/contact-icon.mas, contact => $contact &>
      </td>
% }
      <td>
        <a href="<% $contact->uri() %>"><% $contact->display_name() %></a>
      </td>
      <td>
% if ($address) {
        <% join ', ', grep { ! string_is_empty($_) } $address->city(), $address->region() %>
% }
      </td>
      <td>
% if ( my $address = $contact->preferred_email_address() ) {
        <% $address->email_address() %>
% }
      </td>
      <td class="activity">
% if ( $donation || $note ) {
        <ul>
% }
% if ($donation) {
          <li><a href="<% $contact->uri( view => 'donations' ) %>"
                 title="All donations from this contact">Donated</a> <% format_money( $donation->amount() ) %>
              on <% $c->user()->format_date( $donation->donation_date() ) %></li>
% }
% if ($note) {
          <li><% $note->user()->display_name() %>
              <a href="<% $contact->uri( view => 'notes' ) %>"
                 title="All notes about this contact"><% lcfirst $note->type()->description() %></a>
              on <% $c->user()->format_datetime( $note->note_datetime() ) %>:
              <% format_note( $note->truncated_note() ) | n %></li>
% }
% if ( $donation || $note ) {
        </ul>
% }
      </td>
      <td>
        <% $c->user()->format_datetime( $contact->creation_datetime() ) %>
      </td>
      <td>
        <% $c->user()->format_datetime( $contact->last_modified_datetime() ) %>
      </td>
% if ($can_edit) {
      <td>
        <a class="action-button small"  href="<% $contact->uri( view => 'edit_form' ) %>">Edit</a>
      </td>
      <td>
        <a class="action-button small" href="<% $contact->uri( view => 'deletion_confirmation' ) %>">Delete</a>
      </td>
% }
    </tr>
  </tbody>

<%args>
$contact
$row_class
$search
$can_edit
</%args>

<%init>
my $address = $contact->preferred_address();
my $tags = $contact->tags();
my $donation = $contact->contact()->donations()->next();
my $note = $contact->contact()->notes()->next();
</%init>

</%def>
