#!/usr/bin/perl

use strict;
use warnings;
use autodie qw( :all );

use lib 'lib', 'inc';

use FindBin;
use MIME::Base64 qw( encode_base64 );
use R2::DatabaseManager;
use Storable qw( nfreeze );

my $db_name = $ENV{JENKINS_URL} ? "R2Jenkins_$ENV{BUILD_NUMBER}" : 'R2Test';

R2::DatabaseManager->new(
    db_name          => $db_name,
    drop             => 1,
    seed             => 0,
    quiet            => 1,
    _existing_config => {},         # XXX - uber hack!
)->update_or_install_db();

require R2::Config;

R2::Config->instance()->_set_database_name($db_name);

require R2::Schema;

open my $fh, '>', 't/lib/R2/Test/FakeSchema.pm';

print {$fh}
    sprintf( <<'EOF', encode_base64( nfreeze( R2::Schema->Schema() ) ) );
package R2::Test::FakeSchema;

use strict;
use warnings;

use MIME::Base64 qw( decode_base64 );
use Storable qw( thaw );

$R2::Schema::TestSchema = thaw( decode_base64( '%s' ) );

1;
EOF
